#!/usr/bin/nu

const kernel = "/boot/vmlinuz-linux-cachyos"
const image = "/boot/initramfs-linux-cachyos.img"
const uki = "/boot/EFI/Linux/arch-linux-cachyos.efi"

let kernel_hash: string = sha512sum $kernel
let image_hash: string = sha512sum $image
let uki_hash: string = sha512sum $uki


try {
	print "pacman..."
	sudo pacman -Syu
	print ""
	
	
	print "AUR..."
	yay -Syua --devel --removemake
	print ""
	
	
	print "flatpak..."
	flatpak update
	print ""
}


print "updates done, checking for packages to remove:"


print "pacman: debug..."
let debugPackages = pacman -Qqs -- '-debug' | complete
if ($debugPackages.exit_code == 0) {
	sudo pacman -Rns ...($debugPackages.stdout | split row "\n" | drop 1)
} else {
	print "no debug packages"
}
print ""


print "pacman: orphans..."
let orphanPackages = pacman -Qqdt | complete
if ($orphanPackages.exit_code == 0) {
	sudo pacman -Rns ...($orphanPackages.stdout | split row "\n" | drop 1)
} else {
	print "no orphans"
}
print ""


print "flatpak..."
flatpak uninstall --unused


let new_kernel_hash: string = sha512sum $kernel
let new_image_hash: string = sha512sum $image
let new_uki_hash: string = sha512sum $uki

if ($kernel_hash != $new_kernel_hash or $image_hash != $new_image_hash or $uki_hash != $new_uki_hash) {
	print ""
	print "\e[33mKernel has been updated!\e[0m\n"
}
